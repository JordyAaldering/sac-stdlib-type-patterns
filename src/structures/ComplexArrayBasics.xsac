module ComplexArrayBasics;

use ComplexBasics: { complex };

#define OVERLOADS dim,shape,sel,reshape,genarray,modarray
use Array: all except { OVERLOADS };
import Array: { OVERLOADS };

export all;

#include "Templates.mac"

/******************************************************************************
 *
 * @fn int dim( complex[*] A)
 *
 * @brief yields an array's dimensionality (rank).
 *
 ******************************************************************************/
inline
int dim( complex[*] A)
{
  res = dim( (double[+])A) - 1;
  return( res);
}

/******************************************************************************
 *
 * @fn int[.] shape( complex[*] A)
 *
 * @brief yields an array's shape vector.
 *
 ******************************************************************************/
inline
int[.] shape( complex[*] A)
{
  res = drop( [-1], shape( (double[+])A));
  return( res);
}

/******************************************************************************
 *
 * @fn complex[d:shp] sel( int[vlen] idx, complex[vlen:vshp,d:shp] A)
 *
 * @brief generalizes selections for scalar indices.
 *
 ******************************************************************************/
inline
complex[d:shp] sel( int[vlen] idx, complex[vlen:vshp,d:shp] A)
  | all( idx < vshp)
{
  v = ((double[+])A)[ idx];
  res = (complex[*])v;
  return( res);
}

inline
complex[d:shp] sel( int idx, complex[n,d:shp] A)
  | idx < n
{
  res = sel( [idx], A);
  return( res);
}

/******************************************************************************
 *
 * @fn double[d:shp] norm( complex[d:shp] A)
 *
 * @brief yields an array of norms.
 *
 ******************************************************************************/
inline
double[d:shp] norm( complex[d:shp] A)
{
  res = with {
          (0*shp <= iv < shp)
            : ComplexBasics::norm( A[ iv]);
        } : genarray( shp, 0d);
  return( res);
}

/******************************************************************************
 *
 * @fn double[d:shp] normSq( complex[d:shp] A)
 *
 * @brief yields an array of normSq.
 *
 ******************************************************************************/
inline
double[d:shp] normSq( complex[d:shp] A)
{
  res = with {
          (0*shp <= iv < shp)
            : ComplexBasics::normSq( A[ iv]);
        } : genarray( shp, 0d);
  return( res);
}

/******************************************************************************
 *
 * @fn complex[d:shp] reshape( int[d:shp] shp, complex[*] A)
 *
 * @brief creates a new array with identical data vector but new shape,
 *        provided the length of the data vector matches the product of
 *        the new shape vector.
 *
 ******************************************************************************/
inline
complex[d:shp] reshape( int[d] shp, complex[o:orig] A)
  | prod( shp) == prod( orig)
{
  v = reshape( shp ++ [2], (double[*])A);
  res = (complex[*])v;
  return( res);
}

/******************************************************************************
 *
 * @fn complex[n:shp,m:vshp] genarray( int[n] shp, complex[m:vshp] val)
 *
 * @brief
 *
 ******************************************************************************/
inline
complex[n:shp,m:vshp] genarray( int[n] shp, complex[m:vshp] val)
{
  res = with {
          (0*shp <= iv < shp) : val;
        } : genarray( shp, val);
  return( res);
}

/******************************************************************************
 *
 * @fn complex[o:outer,i:inner] modarray( complex[o:outer,i:inner] A,
 *                                        int[o] idx, complex[i:inner] val)
 *
 * @brief modifies the element of array at position idx.
 *
 ******************************************************************************/
inline
complex[o:outer,i:inner] modarray( complex[o:outer,i:inner] A,
                                   int[o] idx, complex[i:inner] val)
  | all( idx < outer)
{
  res = with {
          (idx <= iv <= idx) : val;
        } : modarray( A);
  return( res);
}

inline
complex[n,d:shp] modarray( complex[n,d:shp] A, int idx, complex[d:shp] val)
  | idx < n
{
  res = modarray( A, [idx], val);
  return( res);
}
